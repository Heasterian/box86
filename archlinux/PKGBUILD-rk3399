# Maintainer: SpacingBat3 <git@spacingbat3.anonaddy.com>
# Contributor: Sebastien Chevalier <sebastien.chev@gmail.com>
pkgname=box86-rk3399-git
_pkgname=box86-git
pkgver=r2798.0edc0374
pkgrel=1
pkgdesc="Linux Userspace x86 Emulator with a twist, targeted at ARM Linux devices."
arch=('armv7h')
url="https://github.com/ptitSeb/box86"
license=('MIT')
optdepends=('gl4es: OpenGL 2 for GLES 2 devices')
makedepends=('git' 'cmake' 'make')
provides=("${pkgname%-git}" "${pkgname%-rk3399-git}-git" "${pkgname%-rk3399-git}")
conflicts=("${pkgname%-git}" "${pkgname%-rk3399-git}-git" "${pkgname%-rk3399-git}")
source=('git+https://github.com/ptitSeb/box86')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-rk3399-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${pkgname%-rk3399-git}"
	if [[ ! -d ./build ]]; then
		mkdir build && cd build
		cmake .. -DRK3399=1 -DCMAKE_BUILD_TYPE=RelWithDebInfo
	fi
}

build() {
	cd "$srcdir/${pkgname%-rk3399-git}/build"
	make -j$(nproc)
}

package() {
	cd "$srcdir/${pkgname%-rk3399-git}/build"
	make DESTDIR="${pkgdir}/" install
}
