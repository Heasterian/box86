# Maintainer: SpacingBat3 <git@spacingbat3.anonaddy.com>
# Contributor: Sebastien Chevalier <sebastien.chev@gmail.com>
pkgname=box86-gameshell-git
pkgver=r2798.0edc0374
pkgrel=1
pkgdesc="Linux Userspace x86 Emulator with a twist, targeted at ARM Linux devices."
arch=('armv7h')
url="https://github.com/ptitSeb/box86"
license=('MIT')
optdepends=('gl4es: OpenGL 2 for GLES 2 devices')
makedepends=('git' 'cmake' 'make')
provides=("${pkgname%-git}" "${pkgname%-gameshell-git}-git" "${pkgname%-gameshell-git}")
conflicts=("${pkgname%-git}" "${pkgname%-gameshell-git}-git" "${pkgname%-gameshell-git}")
source=('git+https://github.com/ptitSeb/box86')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-gameshell-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${pkgname%-gameshell-git}"
	if [[ ! -d ./build ]]; then
		mkdir build && cd build
		cmake .. -DGAMESHELL=1 -DCMAKE_BUILD_TYPE=RelWithDebInfo
	fi
}

build() {
	cd "$srcdir/${pkgname%-gameshell-git}/build"
	make -j$(nproc)
}

package() {
	cd "$srcdir/${pkgname%-gameshell-git}/build"
	make DESTDIR="${pkgdir}/" install
	# /usr/local/bin isn't in PATH by the default,
	# we should move it to /usr/bin
	cd ${pkgdir}
	mv usr/local/bin/ usr/bin/
	# cleanup when dir is empty
	rmdir usr/local || exit 0
}
